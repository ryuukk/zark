#ifdef GL_ES
#define LOWP lowp
#define MED mediump
#define HIGH highp
precision mediump float;
#else
#define MED
#define LOWP
#define HIGH
#endif

#if defined(specularTextureFlag) || defined(specularColorFlag)
#define specularFlag
#endif

#ifdef normalFlag
in vec3 v_normal;
#endif //normalFlag

#if defined(colorFlag)
in vec4 v_color;
#endif

#ifdef blendedFlag
in float v_opacity;
#ifdef alphaTestFlag
in float v_alphaTest;
#endif //alphaTestFlag
#endif //blendedFlag

#if defined(diffuseTextureFlag) || defined(specularTextureFlag) || defined(emissiveTextureFlag)
#define textureFlag
#endif

#ifdef textureArrayFlag
in float v_textureIndex;
#endif

#ifdef diffuseTextureFlag
in MED vec2 v_diffuseUV;
#endif

#ifdef specularTextureFlag
in MED vec2 v_specularUV;
#endif

#ifdef emissiveTextureFlag
in MED vec2 v_emissiveUV;
#endif

#ifdef diffuseColorFlag
uniform vec4 u_diffuseColor;
#endif

#ifdef diffuseTextureFlag
#ifdef textureArrayFlag
uniform sampler2DArray u_diffuseTexture;
#else
uniform sampler2D u_diffuseTexture;
#endif
#endif

#ifdef specularColorFlag
uniform vec4 u_specularColor;
#endif

#ifdef specularTextureFlag
uniform sampler2D u_specularTexture;
#endif

#ifdef normalTextureFlag
uniform sampler2D u_normalTexture;
#endif

#ifdef emissiveColorFlag
uniform vec4 u_emissiveColor;
#endif

#ifdef emissiveTextureFlag
uniform sampler2D u_emissiveTexture;
#endif

#ifdef lightingFlag
in vec3 v_lightDiffuse;

#if	defined(ambientLightFlag) || defined(ambientCubemapFlag) || defined(sphericalHarmonicsFlag)
#define ambientFlag
#endif //ambientFlag

#ifdef specularFlag
in vec3 v_lightSpecular;
#endif //specularFlag

#ifdef shadowMapFlag
uniform sampler2D u_shadowTexture;
uniform float u_shadowPCFOffset;
in vec3 v_shadowMapUv;
#define separateAmbientFlag

float getShadowness(vec2 offset)
{
    const vec4 bitShifts = vec4(1.0, 1.0 / 255.0, 1.0 / 65025.0, 1.0 / 16581375.0);
    return step(v_shadowMapUv.z, dot(texture(u_shadowTexture, v_shadowMapUv.xy + offset), bitShifts));//+(1.0/255.0));
}

float getShadow()
{
	return (//getShadowness(vec2(0,0)) +
			getShadowness(vec2(u_shadowPCFOffset, u_shadowPCFOffset)) +
			getShadowness(vec2(-u_shadowPCFOffset, u_shadowPCFOffset)) +
			getShadowness(vec2(u_shadowPCFOffset, -u_shadowPCFOffset)) +
			getShadowness(vec2(-u_shadowPCFOffset, -u_shadowPCFOffset))) * 0.25;
}
#endif //shadowMapFlag

#if defined(ambientFlag) && defined(separateAmbientFlag)
in vec3 v_ambientLight;
#endif //separateAmbientFlag

#endif //lightingFlag

#ifdef fogFlag
uniform vec4 u_fogColor;
in float v_fog;
#endif // fogFlag





out vec4 v_fragColor;

void main() {
	#if defined(normalFlag)
		vec3 normal = v_normal;
	#endif // normalFlag

	#ifdef textureArrayFlag
	
		#if defined(diffuseTextureFlag) && defined(diffuseColorFlag) && defined(colorFlag)
			vec4 diffuse = texture(u_diffuseTexture, vec3(v_diffuseUV, v_textureIndex)) * u_diffuseColor * v_color;
		#elif defined(diffuseTextureFlag) && defined(diffuseColorFlag)
			vec4 diffuse = texture(u_diffuseTexture, vec3(v_diffuseUV, v_textureIndex)) * u_diffuseColor;
		#elif defined(diffuseTextureFlag) && defined(colorFlag)
			vec4 diffuse = texture(u_diffuseTexture, vec3(v_diffuseUV, v_textureIndex)) * v_color;
		#elif defined(diffuseTextureFlag)
			vec4 diffuse = texture(u_diffuseTexture, vec3(v_diffuseUV, v_textureIndex));
		#elif defined(diffuseColorFlag) && defined(colorFlag)
			vec4 diffuse = u_diffuseColor * v_color;
		#elif defined(diffuseColorFlag)
			vec4 diffuse = u_diffuseColor;
		#elif defined(colorFlag)
			vec4 diffuse = v_color;
		#else
			vec4 diffuse = vec4(1.0);
		#endif
	
	#else

		#if defined(diffuseTextureFlag) && defined(diffuseColorFlag) && defined(colorFlag)
			vec4 diffuse = texture(u_diffuseTexture, v_diffuseUV) * u_diffuseColor * v_color;
		#elif defined(diffuseTextureFlag) && defined(diffuseColorFlag)
			vec4 diffuse = texture(u_diffuseTexture, v_diffuseUV) * u_diffuseColor;
		#elif defined(diffuseTextureFlag) && defined(colorFlag)
			vec4 diffuse = texture(u_diffuseTexture, v_diffuseUV) * v_color;
		#elif defined(diffuseTextureFlag)
			vec4 diffuse = texture(u_diffuseTexture, v_diffuseUV);
		#elif defined(diffuseColorFlag) && defined(colorFlag)
			vec4 diffuse = u_diffuseColor * v_color;
		#elif defined(diffuseColorFlag)
			vec4 diffuse = u_diffuseColor;
		#elif defined(colorFlag)
			vec4 diffuse = v_color;
		#else
			vec4 diffuse = vec4(1.0);
		#endif
	
	#endif
	
	
	#if defined(emissiveTextureFlag) && defined(emissiveColorFlag)
		vec4 emissive = texture(u_emissiveTexture, v_emissiveUV) * u_emissiveColor;
	#elif defined(emissiveTextureFlag)
		vec4 emissive = texture(u_emissiveTexture, v_emissiveUV);
	#elif defined(emissiveColorFlag)
		vec4 emissive = u_emissiveColor;
	#else
		vec4 emissive = vec4(0.0);
	#endif

	#if (!defined(lightingFlag))
		v_fragColor.rgb = diffuse.rgb + emissive.rgb;
	#elif (!defined(specularFlag))
		#if defined(ambientFlag) && defined(separateAmbientFlag)
			#ifdef shadowMapFlag
				v_fragColor.rgb = (diffuse.rgb * (v_ambientLight + getShadow() * v_lightDiffuse)) + emissive.rgb;
				//v_fragColor.rgb = texture(u_shadowTexture, v_shadowMapUv.xy);
			#else
				v_fragColor.rgb = (diffuse.rgb * (v_ambientLight + v_lightDiffuse)) + emissive.rgb;
			#endif //shadowMapFlag
		#else
			#ifdef shadowMapFlag
				v_fragColor.rgb = getShadow() * (diffuse.rgb * v_lightDiffuse) + emissive.rgb;
			#else
				v_fragColor.rgb = (diffuse.rgb * v_lightDiffuse) + emissive.rgb;
			#endif //shadowMapFlag
		#endif
	#else
		#if defined(specularTextureFlag) && defined(specularColorFlag)
			vec3 specular = texture(u_specularTexture, v_specularUV).rgb * u_specularColor.rgb * v_lightSpecular;
		#elif defined(specularTextureFlag)
			vec3 specular = texture(u_specularTexture, v_specularUV).rgb * v_lightSpecular;
		#elif defined(specularColorFlag)
			vec3 specular = u_specularColor.rgb * v_lightSpecular;
		#else
			vec3 specular = v_lightSpecular;
		#endif

		#if defined(ambientFlag) && defined(separateAmbientFlag)
			#ifdef shadowMapFlag
			v_fragColor.rgb = (diffuse.rgb * (getShadow() * v_lightDiffuse + v_ambientLight)) + specular + emissive.rgb;
				//v_fragColor.rgb = texture(u_shadowTexture, v_shadowMapUv.xy);
			#else
				v_fragColor.rgb = (diffuse.rgb * (v_lightDiffuse + v_ambientLight)) + specular + emissive.rgb;
			#endif //shadowMapFlag
		#else
			#ifdef shadowMapFlag
				v_fragColor.rgb = getShadow() * ((diffuse.rgb * v_lightDiffuse) + specular) + emissive.rgb;
			#else
				v_fragColor.rgb = (diffuse.rgb * v_lightDiffuse) + specular + emissive.rgb;
			#endif //shadowMapFlag
		#endif
	#endif //lightingFlag

	#ifdef fogFlag
		v_fragColor.rgb = mix(v_fragColor.rgb, u_fogColor.rgb, v_fog);
	#endif // end fogFlag

	#ifdef blendedFlag
		v_fragColor.a = diffuse.a * v_opacity;
		#ifdef alphaTestFlag
			if (v_fragColor.a <= v_alphaTest)
				discard;
		#endif
	#else
		v_fragColor.a = 1.0;
	#endif
	
	//float intensity = max(v_fragColor.r, max(v_fragColor.g, v_fragColor.b));
	//float factor;
	//
	//if (intensity > 0.8)
	//factor = 1.0;
	//else if (intensity > 0.6)
	//factor = 0.8;
	//else if (intensity > 0.3)
	//factor = 0.4;
	//else
	//factor = 0.2;
    //
	//v_fragColor.rgb *= factor;

}
